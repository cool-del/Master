if (MoudleDebug) {
    apply plugin: 'com.android.application'
    apply from: '../app/tinkerpatch.gradle'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        vectorDrawables.useSupportLibrary = true
        if (MoudleDebug) {
            applicationId "com.ooftf.${project.getName()}"
            multiDexEnabled true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable", DOCKING_MODULE_NAME: project.getName()]
            }
        }
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }
    sourceSets {
        main {
            if (MoudleDebug) {
                manifest.srcFile 'src/app/AndroidManifest.xml'
                java.srcDirs = ['src/app/java', 'src/main/java']
                res.srcDirs = ['src/app/res', 'src/main/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java.srcDirs = ['src/main/java']
                res.srcDirs = ['src/main/res']
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation deps.constraintLayout
    kapt "com.alibaba:arouter-compiler:$versions.arouter"
    api project(':service')

}


